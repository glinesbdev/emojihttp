📦 json 🏠

💭 A wrapper around an HTTP request

🌍 🐇 🔶🌐🗺️ 🍇
  🖍️🆕 url 🔡
  🖍️🆕 path 🔡
  🖍️🆕 port 🔢
  🖍️🆕 method 🔡
  🖍️🆕 headers 🔡
  🖍️🆕 response 🔡
  🖍️🆕 body 🔡

  🆕 🍼 method 🔡 🍼 url 🔡 🍼 port 🔢 🍼 path 🔡 🍼 body 🔡 🍼 headers 🔡 🍇
    🔤🔤 ➡️ 🖍️response
  🍉

  🆕 🍼 method 🔡 🍼 url 🔡 🍼 path 🔡 🍼 body 🔡 🍼 headers 🔡 🍇
    80 ➡️ 🖍️port
    🔤🔤 ➡️ 🖍️response
  🍉

  🆕 🍼 url 🔡 🍼 path 🔡 🍼 headers 🔡 🍇
    🔤GET🔤 ➡️ 🖍️method
    80 ➡️ 🖍️port
    🔤🔤 ➡️ 🖍️body
    🔤🔤 ➡️ 🖍️response
  🍉

  🆕 🍼 method 🔡 🍼 url 🔡 🍼 path 🔡 🍼 body 🔡 🍇
    80 ➡️ 🖍️port
    🔤🔤 ➡️ 🖍️headers
    🔤🔤 ➡️ 🖍️response
  🍉

  🆕 🍼 method 🔡 🍼 url 🔡 🍼 path 🔡 🍇
    80 ➡️ 🖍️port
    🔤🔤 ➡️ 🖍️body
    🔤🔤 ➡️ 🖍️headers
    🔤🔤 ➡️ 🖍️response
  🍉

  🆕 🍼 url 🔡 🍼 path 🔡 🍇
    🔤GET🔤 ➡️ 🖍️method
    80 ➡️ 🖍️port
    🔤🔤 ➡️ 🖍️body
    🔤🔤 ➡️ 🖍️headers
    🔤🔤 ➡️ 🖍️response
  🍉

  🆕 🍼 url 🔡 🍇
    🔤GET🔤 ➡️ 🖍️method
    80 ➡️ 🖍️port
    🔤/🔤 ➡️ 🖍️path
    🔤🔤 ➡️ 🖍️body
    🔤🔤 ➡️ 🖍️headers
    🔤🔤 ➡️ 🖍️response
  🍉

  ❗🕳️ _port 🔢 🍇
    _port ➡️ 🖍️port
  🍉

  ❓🕳️ ➡️ 🔢 🍇
    ↩️ port
  🍉

  ❗🚀 🚧🚧🔸↕️ 🍇
    🆕🔶🌐🧦 method url port path headers body❗ ➡️ sock
    🔺☎️sock❗ ➡️ socket
    🔺📖sock socket❗
    🔺👂socket 10000❗ ➡️ data

    ↪️ 🔡data❗ ➡️ data_str 🍇
      ↪️ 📐data_str❗ 🙌 0 🤝 port 🙌 80 🍇
        🕳️👇 443❗
        🔺🚀👇❗
      🍉
      🙅 🍇
        data_str ➡️ 🖍️response
      🍉
    🍉

    🚪socket❗
  🍉

  ❗💪 ➡️ 🔡 🍇
    ↪️ 📐response❗ ▶️ 0 🍇
      ↩️🐽🔫response 🔤❌r❌n❌r❌n🔤❗ 1❗
    🍉

    ↩️ 🔤🔤
  🍉

  ❗🍯 json_str 🔡 ➡️ 🍯🐚⚪🍆 🍇
    ↩️ 🍺🔲 🍺⚪🕊️🌸 json_str❗🍯🐚⚪🍆
  🍉
🍉
